WHITESPACE = _{ " " | "\n" | "\t" }
program = { SOI ~ term ~ EOI }

variable = @{ !(keyword ~ WHITESPACE) ~  (ASCII_ALPHA_LOWER | "_") ~ (ASCII_ALPHA|ASCII_DIGIT|"_")* }
type_variable = @{ !(keyword ~ WHITESPACE) ~  ASCII_ALPHA_UPPER ~ (ASCII_ALPHA|ASCII_DIGIT|"_")* }
number = { ASCII_DIGIT+ }


// Keyword 
kw_unit = _{ "unit" }
kw_fix = _{ "fix" }
kw_let = _{ "let" }
kw_as = _{ "as" }
kw_in = _{ "in" }
kw_try = _{ "try" }
kw_with = _{ "with" }
kw_catch = _{ "catch" }
kw_raise = _{ "raise" }
kw_error = _{ "error" }
kw_true = _{ "true" } 
kw_false = _{ "false" }
kw_zero = _{ "zero" }
kw_succ = _{ "succ" }
kw_pred = _{ "pred" }
kw_iszero = _{ "iszero" }
kw_case = _{ "case" }
kw_of = _{ "of" }
kw_mu = _{ "mu" }
kw_if = _{ "if" }
kw_else = _{ "else" }
kw_fold = _{ "fold" }
kw_unfold = _{ "unfold" }
kw_forall = _{ "forall" }
kw_exists = _{ "exists" }
kw_ref = _{ "ref" }
kw_something = _{ "Something" }
kw_nothing = _{ "Nothing" }
kw_inl = _{ "inl" }
kw_inr = _{ "inr" }
kw_nil = _{ "Nil" }
kw_cons = _{ "Cons" }
kw_head = _{ "head" }
kw_tail = _{ "tail" }
kw_isnil = _{ "isnil" }
kw_fst = _{ "fst" }
kw_snd = _{ "snd" }

kw_tytop = _{ "Top" }
kw_tyunit = _{ "Unit" }
kw_tynat = _{ "Nat" }
kw_tybool = _{ "Bool" }
kw_tyref = _{ "Ref" }
kw_tysink = _{ "Sink" }
kw_tysource = _{ "Source" }
kw_tylist  = _{ "List" }

keyword = _{
  | kw_unit
  | kw_fix
  | kw_try
  | kw_with 
  | kw_raise
  | kw_error 
  | kw_let
  | kw_as
  | kw_in
  | kw_true 
  | kw_false
  | kw_succ 
  | kw_pred
  | kw_iszero
  | kw_case
  | kw_of
  | kw_mu
  | kw_if
  | kw_else
  | kw_fold
  | kw_unfold
  | kw_forall
  | kw_exists
  | kw_ref
  | kw_something
  | kw_nothing
  | kw_inl
  | kw_inr
  | kw_nil
  | kw_cons
  | kw_head
  | kw_tail
  | kw_isnil
  | kw_fst 
  | kw_snd

  | kw_tyunit
  | kw_tynat
  | kw_tybool
  | kw_tyref
}

// Terms 
term = { prim_term ~ left_rec? }
paren_term = { "(" ~ term ~ ")" }

prim_term = {
  | const_term 
  | fix_term
  | let_term
  | untyped_lambda_term
  | lambda_term
  | lambda_sub_term
  | ty_lambda_star_term
  | ty_lambda_kinded_term
  | pack_term
  | unpack_term
  | record_term
  | succ_term
  | pred_term
  | iszero_term
  | if_term
  | try_term 
  | try_catch
  | err_term
  | raise_term 
  | fold_term
  | unfold_term
  | pair_term
  | variant_term
  | somecase_term
  | sumcase_term
  | variantcase_term
  | listcase_term
  | ref_term
  | deref_term
  | some_term
  | none_term
  | tuple_term
  | left_term
  | right_term
  | nil_term
  | cons_term
  | head_term
  | tail_term
  | isnil_term
  | paren_term
  | number
  | variable 

}

const_term = { kw_unit | kw_zero | kw_true | kw_false } 
fix_term = { kw_fix ~ paren_term }
if_term = { kw_if ~ "(" ~ term ~ ")" ~ "{" ~ term ~ "}" ~ kw_else ~ "{" ~ term ~ "}" }
let_term = { kw_let ~ "(" ~ variable ~ "=" ~ term ~ ")" ~ kw_in ~ term }
pair_term = { "{" ~ term ~ "," ~ term ~ "}" }
record_term = { "{" ~ variable ~ "=" ~ term ~ ("," ~ variable ~ "=" ~ term)* ~ "}" }
tuple_term = { "(" ~ term ~ "," ~ term? ~ ("," ~ term)* ~ ")" }

untyped_lambda_term = { "\\" ~ variable ~ "." ~ term }
lambda_term = { "\\" ~ variable ~ ":" ~ type ~ "." ~ term }

lambda_sub_term = { "\\" ~ type_variable ~ "<:" ~ type ~ "." ~ term }
ty_lambda_star_term = { "\\" ~ type_variable ~ "." ~ term }
ty_lambda_kinded_term = { "\\" ~ type_variable ~ "::" ~ kind ~ "." ~ term }

pack_term = { "{" ~ "*" ~ type ~ "," ~ term ~ "}" ~ kw_as ~ type }
unpack_term = { kw_let ~ "{" ~ type_variable ~ "," ~ variable ~ "}" ~ "=" ~ term ~ kw_in ~ term }

succ_term = { kw_succ ~ paren_term }
pred_term = { kw_pred ~ paren_term }
iszero_term = { kw_iszero ~ paren_term }

try_term = { kw_try ~ "{" ~ term ~ "}" ~ kw_with ~ "{" ~ term ~ "}" }
try_catch = { kw_try ~ "{" ~ term ~ "}" ~ kw_catch ~ "{" ~ term ~ "}" }
err_term = { kw_error ~ "[" ~ type ~ "]" }
raise_term = { kw_raise ~ "[" ~ type ~ "," ~ type ~ "]" ~ "(" ~ term ~ ")" } 

fold_term = { kw_fold ~ "[" ~ type ~ "]" ~ term }
unfold_term = { kw_unfold ~ "[" ~ type ~ "]" ~ term }

some_term = { kw_something ~ paren_term } 
none_term = { kw_nothing ~ "[" ~ type ~ "]" } 
somecase_term = { kw_case ~ term ~ kw_of ~ "{" ~ somecase_patterns ~ "}" }
somecase_patterns = _{ (some_pattern ~ "|" ~ none_pattern) | (none_pattern ~ "|" ~ some_pattern) }
none_pattern = { kw_nothing ~ "=>" ~ term }
some_pattern = { kw_something ~ "(" ~ variable ~ ")" ~ "=>" ~ term }

left_term = { kw_inl ~ paren_term ~ kw_as ~ type } 
right_term = { kw_inr ~ paren_term ~ kw_as ~ type }
sumcase_term = { kw_case ~ term ~ kw_of ~ "{" ~ sumcase_patterns ~ "}" }
sumcase_patterns = _{ (left_pattern ~ "|" ~ right_pattern) | (right_pattern ~ "|" ~ left_pattern) }
left_pattern = { kw_inl ~ "(" ~ variable ~ ")" ~ "=>" ~ term } 
right_pattern = { kw_inr ~ "(" ~ variable ~ ")" ~ "=>" ~ term }

variant_term = { "<" ~ variable ~ "=" ~ term ~ ">" ~ kw_as ~ type}
variantcase_term = { kw_case ~ term ~ kw_of ~ "{" ~ variant_pattern ~ ("|" ~ variant_pattern)* ~ "}" }
variant_pattern = { "<" ~ variable ~ "=" ~ variable ~ ">" ~ "=>" ~ term }

nil_term = { kw_nil ~ "[" ~ type ~ "]" }
cons_term = { kw_cons ~ "[" ~ type ~ "]" ~ "(" ~ term ~ "," ~ term ~ ")" } 
head_term = { kw_head ~ "[" ~ type ~ "]" ~ "(" ~ term ~ ")" }
tail_term = { kw_tail ~ "[" ~ type ~ "]" ~ "(" ~ term ~ ")" }
isnil_term = { kw_isnil ~ "[" ~ type ~ "]" ~ "(" ~ term ~ ")" }
listcase_term = { kw_case ~ term ~ kw_of ~ "{" ~ listcase_patterns ~ "}" }
listcase_patterns = _{ (nil_pattern ~ "|" ~ cons_pattern) | (cons_pattern ~ "|" ~ nil_pattern) }
nil_pattern = { kw_nil ~ "=>" ~ term}
cons_pattern = { kw_cons ~ "(" ~ variable ~ "," ~ variable ~ ")" ~ "=>" ~ term }

deref_term = { "!" ~ term }
ref_term = { kw_ref ~ paren_term }

left_rec = { tyapp | fst_term | snd_term | projection | record_proj |  sequence | assign | ascription | cast | term }
record_proj = { "." ~ variable } 
tyapp = { "[" ~ type ~ "]" }
fst_term = @{ "." ~ kw_fst ~ !(ASCII_ALPHA | ASCII_DIGIT | "_") }
snd_term = @{ "." ~ kw_snd ~ !(ASCII_ALPHA | ASCII_DIGIT | "_") } 
projection = { "." ~ number }
sequence = { ";" ~ term }
assign = { ":=" ~ term }
ascription = { ":" ~ type }
cast = { kw_as ~ type } 


// Types
paren_type = {"(" ~ type ~ ")"}
type = { prim_type ~ left_rec_type? }

prim_type = {
  | const_type 
  | top_type
  | top_type_star
  | prod_type
  | forall_bounded_type
  | forall_unbounded_type
  | forall_kinded_type
  | exists_bounded_type
  | exists_unbounded_type
  | exists_kinded_type
  | op_lambda_type
  | op_lambda_sub_type
  | op_lambda_star_type
  | sum_type
  | record_type
  | tuple_type
  | variant_type
  | mu_type
  | ref_type
  | source_type
  | sink_type 
  | list_type
  | type_variable
  | paren_type
}

ty_arg = _{ "[" ~ type ~ "]" }

const_type = { kw_tybool | kw_tynat | kw_tyunit }
top_type  = { kw_tytop ~ "[" ~ kind ~ "]" }
top_type_star = { kw_tytop }

forall_bounded_type = { forall_start ~ "<:" ~ type ~ "." ~ type } 
forall_unbounded_type = { forall_start ~ "." ~ type }
forall_kinded_type = { forall_start ~ "::" ~ kind ~ "." ~ type } 
forall_start = ${ kw_forall ~ " " ~ type_variable }

exists_bounded_type ={ "{" ~ exists_start ~ "<:" ~ type ~ "," ~ type ~ "}"  } 
exists_unbounded_type = { "{" ~ exists_start ~ "," ~ type ~ "}" }
exists_kinded_type = { "{" ~ exists_start ~ "::" ~ kind ~ "," ~ type ~ "}" }
exists_start = ${ kw_exists ~ " " ~ type_variable }

op_lambda_star_type = { "\\" ~ type_variable ~ "." ~ type }
op_lambda_type = { "\\" ~ type_variable ~ "::" ~ kind ~ "." ~ type }
op_lambda_sub_type = { "\\" ~ type_variable ~ "<:" ~ type ~ "." ~ type }

prod_type = { "(" ~ type ~ "x" ~ type ~ ")" }
sum_type = { "(" ~ type ~ "+" ~ type ~ ")" }
record_type =  { "{" ~ variable ~ ":" ~ type ~ ("," ~ variable ~ ":" ~ type)* ~ "}" }
variant_type = { "<" ~ variable ~ ":" ~ type ~ ("," ~ variable ~ ":" ~ type)* ~ ">" }
tuple_type = { "(" ~ type ~ ("," ~ type)+ ~ ")" }
mu_type = { kw_mu ~ type_variable ~ "." ~ type }
list_type = { kw_tylist ~ ty_arg }

ref_type = { kw_tyref ~ ty_arg }
source_type = { kw_tysource ~ ty_arg }
sink_type = { kw_tysink ~ ty_arg } 

left_rec_type = { fun_type | op_app_type }
fun_type = { "->" ~ type }
op_app_type = { "[" ~ type ~ "]" }

//Kinds
kind = { prim_kind ~ arrow_kind? } 
prim_kind = { 
  | paren_kind
  | star_kind
}

paren_kind = { "(" ~ kind ~ ")" }
star_kind = { "*" }
arrow_kind = { "=>" ~ kind } 
