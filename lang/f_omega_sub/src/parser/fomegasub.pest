WHITESPACE = _{ " " | "\n" | "\t" }
variable = @{ !(keyword ~ !ASCII_ALPHANUMERIC) ~ ( !(WHITESPACE) ~ (ASCII_ALPHA|ASCII_DIGIT) )+ | "_" }
number = { ASCII_DIGIT+ }

program = { SOI ~ term ~ EOI }

//Keywords
kw_as = _{ "as" }
kw_in = _{ "in" }
kw_let = _{ "let" }
kw_forall = _{ "forall" }
kw_exists = _{ "exists" }
kw_zero = _{ "zero" }
kw_succ = _{ "succ" }
kw_pred = _{ "pred" }
kw_tynat = _{ "Nat" }
kw_tytop = _{ "Top" }
keyword = _{ 
  | kw_as
  | kw_in 
  | kw_let 
  | kw_forall 
  | kw_exists
  | kw_zero
  | kw_succ
  | kw_pred
  | kw_tytop
}

//Terms 
term = { prim_term ~ left_rec? }
prim_term = {
  | paren_term
  | const_term
  | lambda_term 
  | let_term
  | succ_term
  | pred_term
  | tylambda_term
  | tylambda_unbounded
  | pack_term
  | unpack_term
  | record_term
  | number
  | variable
}

paren_term = { "(" ~ term ~ ")" }
const_term = { kw_zero }
lambda_term = { "\\" ~ variable ~ ":" ~ type ~ "." ~ term }
let_term = { kw_let ~ "(" ~ variable ~ "=" ~ term ~ ")" ~ kw_in ~ term }
succ_term = { kw_succ ~ paren_term }
pred_term = { kw_pred ~ paren_term }
tylambda_term = { "\\" ~ variable ~ "<:" ~ type ~ "." ~ term }
tylambda_unbounded = { "\\" ~ variable ~ "::" ~ kind ~ "." ~ term }
pack_term = { "{" ~ "*" ~ type ~ "," ~ term ~ "}" ~ kw_as ~ type }
unpack_term = { kw_let ~ "{" ~ variable ~ "," ~ variable ~ "}" ~ "=" ~ term ~ kw_in ~ term}
record_term = { "{" ~ variable ~ "=" ~ term ~ ("," ~ variable ~ "=" ~ term)* ~ "}" }

left_rec = { ty_app | record_projection | term }
ty_app = { "[" ~ type ~ "]" }
record_projection = { "." ~ variable }

//Types
type = { prim_type ~ left_rec_ty? }
prim_type = { 
  | paren_type 
  | const_ty 
  | top_ty
  | forall_ty 
  | forall_unbounded
  | op_lambda_star
  | op_lambda
  | exists_ty
  | exists_unbounded
  | record_ty
  | variable 
}

paren_type = { "(" ~ type ~ ")" }
const_ty = { kw_tynat }
top_ty = { kw_tytop ~ "::" ~ kind }
forall_ty = { forall_start ~ "<:" ~ type ~ "." ~ type } 
forall_unbounded = { forall_start ~ "::" ~ kind ~ "." ~ type }
forall_start = ${ kw_forall ~ " " ~ variable } 
op_lambda_star = { "\\" ~ variable ~ "." ~ type }
op_lambda = { "\\" ~ variable ~ "::" ~ kind ~ "." ~ type }
exists_ty = { "{" ~ exists_start ~ "<:" ~ type ~ "," ~ type ~ "}" } 
exists_unbounded = { "{" ~ exists_start ~ "::" ~ kind ~ "," ~ type ~ "}" }
exists_start = ${ kw_exists ~ " " ~ variable } 
record_ty = { "{" ~ variable ~ ":" ~ type ~ ("," ~ variable ~ ":" ~ type)* ~ "}" }

left_rec_ty = { fun_ty | op_app }
fun_ty = { "->" ~ type }
op_app = { "[" ~ type ~ "]" }

//Kinds
kind = { prim_kind ~ arrow_kind? } 
prim_kind = { 
  | paren_kind
  | star_kind
}

paren_kind = { "(" ~ kind ~ ")" }
star_kind = { "*" }
arrow_kind = { "=>" ~ kind } 
