WHITESPACE = _{ " " | "\n" | "\t" }
program = { SOI ~ term ~ EOI }

// Keywords 
kw_unit = { "unit" }
kw_ref = { "ref" }
kw_tyref = { "Ref" }
kw_tyunit = { "Unit" }

keyword = { 
 | kw_unit 
 | kw_ref
 | kw_tyunit
 | kw_tyref
 }

// Terms 
term = { prim_term ~ left_rec? }
paren_term = { "(" ~ term ~ ")" }

const = { kw_unit }

prim_term = {
  | paren_term
  | const 
  | lambda_term
  | ref_term
  | deref_term
  | loc_term
  | variable 
}

loc_term = { "l" ~ number } 
deref_term = { "!" ~ term }
ref_term = { kw_ref ~ paren_term }
lambda_term = { "\\" ~ variable ~ ":" ~ type ~ "." ~ paren_term }

// Left Recursive Terms
left_rec = { assign_term | term }

assign_term = { ":=" ~ term }

variable = { !(keyword ~ !ASCII_ALPHANUMERIC) ~ ( !(WHITESPACE) ~ (ASCII_ALPHA|ASCII_DIGIT) )+ }
number = { ASCII_DIGIT+ }

// Types 
type = {
 | prim_type
 | fun_type
 | ref_type
}

prim_type = { kw_tyunit }
fun_type = { "(" ~ type ~ "->" ~ type ~ ")" }
ref_type = { kw_ref ~ type }
