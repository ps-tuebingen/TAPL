WHITESPACE = _{ " " | "\n" | "\t" }
variable = @{ !(keyword ~ !ASCII_ALPHANUMERIC) ~ ( !(WHITESPACE) ~ (ASCII_ALPHA|ASCII_DIGIT) )+ | "_" }

program = { SOI ~ term ~ EOI }

// keywords 
kw_forall = { "forall" }
keyword = { kw_forall } 

// Terms 
term = { prim_term ~ left_rec? }

prim_term = {
 | paren_term
 | lambda_term
 | tylambda_term
 | variable 
}
paren_term = { "(" ~ term ~ ")" }

lambda_term = { "\\" ~ variable ~ ":" ~ type ~ "." ~ term }
tylambda_term = { "\\" ~ variable ~ "." ~ term }

left_rec = { tyapp | term }
tyapp = { "[" ~ type ~ "]" } 

// Types 
type = { prim_type ~ left_rec_ty? }

prim_type = {
  | forall_ty
  | paren_ty
  | variable 
}

paren_ty = { "(" ~ type ~ ")" }
forall_ty = { forall_var ~ "." ~ type } 
forall_var = ${kw_forall ~ " " ~ variable }

left_rec_ty = { fun_ty }
fun_ty = { "->" ~ type } 
